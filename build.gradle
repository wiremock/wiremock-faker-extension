buildscript {
  repositories {
    maven {
      url "https://oss.sonatype.org"
    }
    mavenCentral()
  }
}

plugins {
  id 'idea'
  id 'eclipse'
  id 'project-report'
  id 'com.diffplug.spotless' version '6.25.0'
  id 'org.wiremock.tools.gradle.wiremock-extension-convention' version '0.2.0'
}

repositories {
  mavenLocal()
  mavenCentral()
}

projects {
  ext {
    versions = [
      wiremock: "3.5.2"
    ]
  }
}

allprojects {
  apply plugin: 'com.diffplug.spotless'
  spotless {
    java {
      target 'src/**/*.java'
      googleJavaFormat('1.17.0')
      ratchetFrom 'origin/main'
      trimTrailingWhitespace()
      endWithNewline()
      targetExclude '**/Tmp*.java'
    }
    groovyGradle {
      target '**/*.gradle'
      greclipse()
      indentWithSpaces(2)
      trimTrailingWhitespace()
      endWithNewline()
    }
    json {
      target 'src/**/*.json'
      targetExclude '**/tmp*.json', 'src/test/resources/sample.json', 'src/main/resources/swagger/*.json', 'src/test/resources/filesource/subdir/deepfile.json', 'src/test/resources/schema-validation/*.json'
      simple().indentWithSpaces(2)
    }
  }
}

dependencies {
  api "org.wiremock:wiremock:$versions.wiremock"
  //  1.x is not maintained but we need to use to support Java 11. It is in a better state than java-faker, which is also not maintained. Bump to 2.x once we deprecate support for Java 11.
  implementation 'net.datafaker:datafaker:1.9.0'

  testImplementation("org.wiremock:wiremock:$versions.wiremock") {
    artifact {
      classifier = 'tests'
    }
  }
}

shadowJar {
  relocate "com.github.jknack", 'wiremock.com.github.jknack'
  relocate "net.datafaker", 'wiremock.net.datafaker'
}

test {
  useJUnitPlatform()
  testLogging {
    events "PASSED", "FAILED", "SKIPPED"
    exceptionFormat "full"
  }
}
