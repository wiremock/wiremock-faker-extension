buildscript {
  repositories {
    maven {
      url "https://oss.sonatype.org"
    }
    mavenCentral()
  }
}

plugins {
  id 'idea'
  id 'eclipse'
  id 'project-report'
  id 'com.diffplug.spotless' version '6.25.0'
  id 'org.wiremock.tools.gradle.java' version '0.6.0-beta.3'
  id 'org.wiremock.tools.gradle.publishing' version '0.6.0-beta.3'
}

repositories {
  mavenLocal()
  mavenCentral()
}

java {
  sourceCompatibility = 17
  targetCompatibility = 17
}

allprojects {
  apply plugin: 'com.diffplug.spotless'
  spotless {
    java {
      target 'src/**/*.java'
      googleJavaFormat('1.17.0')
      ratchetFrom 'origin/main'
      trimTrailingWhitespace()
      endWithNewline()
      targetExclude '**/Tmp*.java'
    }
    groovyGradle {
      target '**/*.gradle'
      greclipse()
      indentWithSpaces(2)
      trimTrailingWhitespace()
      endWithNewline()
    }
    json {
      target 'src/**/*.json'
      targetExclude '**/tmp*.json', 'src/test/resources/sample.json', 'src/main/resources/swagger/*.json', 'src/test/resources/filesource/subdir/deepfile.json', 'src/test/resources/schema-validation/*.json'
      simple().indentWithSpaces(2)
    }
  }
}

dependencies {
  api "org.wiremock:wiremock-core:$versions.wiremock"
  api 'com.github.jknack:handlebars:4.5.0'
  implementation 'net.datafaker:datafaker:2.5.3'

  testImplementation platform('org.junit:junit-bom:5.14.1')
  testImplementation 'org.junit.jupiter:junit-jupiter-api'
  testImplementation 'org.junit.jupiter:junit-jupiter-params'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter'
  testRuntimeOnly 'org.junit.platform:junit-platform-engine:1.14.1'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.14.1'
  testImplementation("org.wiremock:wiremock:$versions.wiremock") {
    artifact {
      classifier = 'tests'
    }
  }
  testImplementation(testFixtures("org.wiremock:wiremock:$versions.wiremock"))
  testImplementation("org.wiremock:wiremock-junit5:$versions.wiremock")
  testImplementation('org.hamcrest:hamcrest:3.0')
}

shadowJar {
  relocate "com.github.jknack", 'wiremock.com.github.jknack'
  relocate "net.datafaker", 'wiremock.net.datafaker'

  exclude("META-INF/versions/21/**")
}

test {
  useJUnitPlatform()
  testLogging {
    events "PASSED", "FAILED", "SKIPPED"
    exceptionFormat "full"
  }
}

tasks.check {
  dependsOn(buildHealth)
}

dependencyAnalysis {
  issues {
    all {
      onAny {
        severity("fail")
      }
    }
  }
  usage {
    analysis {
      checkSuperClasses(true)
    }
  }
}
